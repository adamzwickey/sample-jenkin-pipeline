buildscript {
    ext {
        springBootVersion = '1.3.7.RELEASE'
        springCloudConnectorsVersion = '1.2.4.BUILD-SNAPSHOT'
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

ext {
  majorVersion = '1.0'
}

apply plugin: 'java'

apply plugin: 'eclipse-wtp'
apply plugin: 'idea'

apply plugin: 'spring-boot'

apply from: 'gradle/pipeline.gradle'
apply plugin: 'maven'

version = getVersion()
group = 'org.cloudfoundry.samples.music'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://repo.spring.io/snapshot" }
}

dependencies {
    // Spring Boot
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-redis"

    // Spring Cloud Connectors
    compile "org.springframework.cloud:spring-cloud-core:${springCloudConnectorsVersion}"
    compile "org.springframework.cloud:spring-cloud-spring-service-connector:${springCloudConnectorsVersion}"
    compile "org.springframework.cloud:spring-cloud-cloudfoundry-connector:${springCloudConnectorsVersion}"

    // JPA Persistence
    compile "commons-dbcp:commons-dbcp"
    runtime "com.h2database:h2"
    runtime "mysql:mysql-connector-java"
    runtime "postgresql:postgresql:9.1-901-1.jdbc4"

    // Redis Persistence
    compile "redis.clients:jedis"

    // Webjars
    compile "org.webjars:bootstrap:3.1.1"
    compile "org.webjars:angularjs:1.2.16"
    compile "org.webjars:angular-ui:0.4.0-2"
    compile "org.webjars:angular-ui-bootstrap:0.10.0-1"
    compile "org.webjars:jquery:2.1.0-2"

    // Testing
    testCompile "junit:junit"
}

jar {
    baseName = project.name
    version = getVersion()
}

def getVersion() {
    return "${majorVersion}.$buildNumber-RC"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${libRepo}") {
              authentication(userName: "${libRepoUsername}", password: "${libRepoPassword}")
            }
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
